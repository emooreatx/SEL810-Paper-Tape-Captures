1  DIM Q[13,13]
10  REM THIS IS A DIRECT TRANSLATION OF DOUG DYMENT'S
11  REM FOCAL PROGRAM--DECUS FOCAL-38. 
20  PRINT "THIS PROGRAM WILL GENERATE AN ODD ORDER MAGIC"
30  PRINT "SQUARE OF THE INDICATED SIZE, USING A SET"
40  PRINT "OF SEQUENTIAL INTEGERS BEGINNING WITH A"
50  PRINT "SPECIFIED STARTING NUMBER."
60  PRINT "THE MAXIMUM SIZE IS 13 BY 13."
100  PRINT 
101  PRINT 
110  PRINT "SIZE";
120  INPUT N
121  IF (N-1)/2=INT(ABS(N-1)/2) THEN 125
122  PRINT "BAD VALUE, TRY AGAIN."
123  GOTO 110
125  IF N>13 THEN 122
130  PRINT "STARTING NUMBER";
140  INPUT S
141  IF INT(S)#S THEN 122
150  PRINT 
200  LET M=N^2+S-1
210  LET C=1
220  LET K=1
230  LET L=(N+1)/2
300  LET Q[K,L]=S
310  LET S=S+1
320  IF M<S THEN 800
400  IF C<N THEN 500
410  LET C=1
420  LET K=K+1
430  GOTO 300
500  LET C=C+1
510  LET K=K-1
520  LET L=L+1
530  IF K=0 THEN 700
600  IF L <= N THEN 300
610  LET L=1
620  GOTO 300
700  LET K=N
710  GOTO 300
800  FOR K=1 TO N
810  PRINT 
811  PRINT 
820  FOR L=1 TO N
825  FOR C=INT(LOG(Q[K,L])/LOG(10)) TO INT(LOG(M)/LOG(10))-1
827  NEXT C
830  PRINT Q[K,L];
840  NEXT L
850  NEXT K
900  PRINT 
905  PRINT 
910  PRINT "THE MAGIC NUMBER =";(2*S-N^2-1)*N/2
920  PRINT 
930  PRINT "DO YOU WANT ANOTHER TRY (IF SO TYPE A 1)";
940  INPUT N
950  IF N=1 THEN 100
999  END 

